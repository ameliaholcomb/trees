apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.chaquo.python'

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.trees.java.activities"
        minSdkVersion 26
        targetSdkVersion 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        // Read the API key from ./secure.properties into R.string.maps_api_key
        def secureProps = new Properties()
        if (file("secure.properties").exists()) {
            file("secure.properties")?.withInputStream { secureProps.load(it) }
        }
        resValue "string", "maps_api_key", (secureProps.getProperty("MAPS_API_KEY") ?: "")
        ndk {
           // abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            abiFilters "arm64-v8a"
        }
        python {
            pip {
                // Pip requirements
                install "numpy"
                install "pillow"
                install "scikit-image"
                install "scikit-learn"
                install "scipy"
                install "grpcio"
                install "tensorboard==2.9.0"
                install "charset-normalizer==2.1.1"
                install "pytorch-lightning==1.5.0"
                install "urllib3==1.26.16"
                install "segmentation-models-pytorch==0.3.2"
            }
            pyc {
                src false
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.17.1'
    implementation 'com.google.ar:core:1.30.0'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'
    implementation 'org.jetbrains:annotations:15.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation 'androidx.work:work-runtime:2.7.1'
    implementation project(path: ':HUAWEI AR Engine SDK_v2.10.0')
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
buildscript {
    ext.kotlin_version = '1.5.30'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
